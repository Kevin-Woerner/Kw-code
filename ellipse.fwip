#    Copyright (C) 2020 by Kevin D. Woerner
# 2020-07-02 kdw  more tests; circ computed
# 2020-06-25 kdw  func renam
# 2020-06-15 kdw  ell[i]psem func
# 2020-06-09 kdw  ell[i]ptic func renam
# 2020-06-01 kdw  flat2ecc and ecc2flat funcs
# 2020-02-21 kdw  created

# 0 <= m == eccentricity**2 <= 1

FUNC DBL ellipsearea(DBL aa, DBL bb):
   # area of an ellipse with semi-axes AA and BB
   RETURN Kw.PI * aa * bb;
FUNC_END

FUNC DBL ellipseecc(DBL aa, DBL bb):
   # eccentricity of an ellipse with semi-axes AA and BB
   DBL rati;

   rati = Kw.ratio(aa, bb);
   RETURN SQRT(1.0 - rati * rati);
FUNC_END

FUNC DBL ellipsem(DBL aa, DBL bb):
   # m parameter of an ellipse with semi-axes AA and BB
   DBL rati;

   rati = Kw.ratio(aa, bb);
   RETURN 1.0 - rati * rati;
FUNC_END

FUNC DBL ellipseflatness(DBL aa, DBL bb):
   # flatness of an ellipse with semi-axes AA and BB
   RETURN 1.0 - Kw.ratio(aa, bb);
FUNC_END

FUNC DBL flat2ecc(DBL flt):
   RETURN SQRT(flt * (2.0 - flt));
FUNC_END

FUNC DBL ecc2flat(DBL ecc):
   RETURN 1.0 - SQRT(1.0 - ecc * ecc);
FUNC_END

FUNC DBL ellipsecir__1(DBL aa, DBL bb):
   # circumference of an ellipse with semi-axes AA and BB
   DBL major_axis;
   DBL minor_axis;
   DBL rati;

   IF aa < bb:
      major_axis = bb;
      minor_axis = aa;
   ELSE:
      major_axis = aa;
      minor_axis = bb;
   IF_END
   rati = minor_axis / major_axis;
   # now major_axis = max(AA, BB)
   RETURN 4.0 * major_axis * Kw.elliptic2(1.0 - rati * rati);
FUNC_END

FUNC DBL ellipsecir(DBL aa, DBL bb):
   # circumference of an ellipse with semi-axes AA and BB
   DBL xx;
   DBL yy;
   DBL mm;
   DBL suma;
   DBL temp;
   INT ii;
   DBL retvalu;

   IF aa < bb:
      xx = bb;
      yy = aa;
   ELSE:
      xx = aa;
      yy = bb;
   IF_END
   IF yy <= SQRT(DBLEPS) * xx:
      retvalu = 4.0 * xx;
   ELSE:
      suma = 0.0;
      mm = 1.0;
      AWAIT Kw.iseq(xx, yy) COUNT ii FROM 0 TO 20:
         temp = (xx + yy) / 2.0;
         yy = SQRT(xx * yy);
         xx = temp;
         mm += mm;
         suma += mm * Kw.square(xx - yy);
      AWAIT_END

      retvalu = PI * (Kw.square(aa + bb) - suma) / (xx + yy);
   IF_END
   RETURN retvalu;
FUNC_END

#TEST:ellipsecir(1,10)==40.639741801008957425577931011816563791;
#TEST:ellipsearea(1,10)==31.41592653589793238462643383279502884;
